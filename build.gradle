plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "com.modrinth.minotaur" version "2.+"
}


group = 'ac.boar'
version = '1.0.4'

repositories {
    mavenCentral()
    maven {
        name = "opencollab-dev"
        url = "https://repo.opencollab.dev/main/"
    }
}

dependencies {
    compileOnly("org.geysermc.geyser:core:2.8.3-SNAPSHOT") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    compileOnly("it.unimi.dsi:fastutil:8.5.15")
    compileOnly("org.projectlombok:lombok:1.18.36")
    annotationProcessor("org.projectlombok:lombok:1.18.36")

    compileOnly("com.google.code.gson:gson:2.3.1")
    testImplementation("com.google.code.gson:gson:2.3.1")

    implementation("it.unimi.dsi:fastutil:8.5.15")

    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.15.2")
}

shadowJar {
    relocate 'it.unimi.dsi.fastutil', 'ac.boar.shaded.fastutil'
    relocate 'com.fasterxml.jackson', 'ac.boar.shaded.jackson'
    relocate 'org.yaml.snakeyaml', 'ac.boar.shaded.snakeyaml'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    versionName = version + "-" + getCommitHash()
    versionNumber = version + "-" + getCommitHash()
    changelog = getCommitMessage()
    projectId = "boar"
    versionType = "alpha"
    uploadFile = shadowJar
    gameVersions = ["1.21.8"]
    loaders = ["bukkit", "paper", "spigot", "bungeecord", "velocity"]
}

def getCommitMessage() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'log', '-1', '--pretty=%B'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

// Thanks to https://gist.github.com/JonasGroeger/7620911 :tm:
def getCommitHash() {
    def gitFolder = "$projectDir/.git/"
    def takeFromHash = 7
    def head = new File(gitFolder + "HEAD").text.split(":")
    def isCommit = head.length == 1

    if(isCommit) return head[0].trim().take(takeFromHash)

    def refHead = new File(gitFolder + head[1].trim())
    refHead.text.trim().take takeFromHash
}